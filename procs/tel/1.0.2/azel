#!/bin/csh -f
#+
#KCWI library of scripts
#
#NAME
#       azel -- move the telescope x arcsec in azimuth and y arcsec in elevation
#
# SYNOPSIS
#	azel x y
#
# DESCRIPTION
#	Move the telescope the given number of arcseconds in the
#	azimuth and elevation directions.
#
# ARGUMENTS
#	x = distance to move in azimuth [arcsec]
#	y = distance to move in elevation [arcsec]
#	- "x" and "y" must be real values
# 
# OPTIONS
#       none
#
# EXAMPLES
#	1) Move the telescope 10 arcseconds in the positive azimuth
#	direction and 20 arcsec in the negative elevation direction:
#		az 10 -20
#
# ENVIRONMENT VARIABLES
#       none
#
# FILES
#     none
#
# SERVERS & KEYWORDS
#     service = dcs
#          keywords: azoff, rel2curr, axestat
#
# EXIT STATUS# Exit values:
#	 0 = normal completion
#	 1 = wrong number of arguments
#
# Example:
#-
# Modification history:
#	2011apr11	MK	Original version
#       2013sep26       MK      changed to use wftel
#-----------------------------------------------------------------------

# check args...
if (${#argv} != 2) then
  echo "Usage: azel x y"
  exit 1
endif

# verify floating-point values...
is_float $* > /dev/null
if ( $status ) then
  echo "ERROR -- Argument must be valid floating-point numbers"
  exit 2
endif

# Grab the current value of autpause...
set autresum = `show -s dcs -terse autresum`

# make a move...
modify -s dcs silent azoff=$1 eloff=$2 rel2curr=t
sleep 3

# wait for guider to cycle twice...
@ startTime = `date +"%s"`
wftel $autresum
@ endTime = `date +"%s"`
@ elapsedTime = $endTime - $startTime
echo "[azel] wftel completed in $elapsedTime sec"
exit
