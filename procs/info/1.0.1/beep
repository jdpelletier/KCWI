#!/bin/csh -f
#+
# beep -- ring the terminal bell one or more times
#
# Purpose:
#	Generates one or more "beep" sounds on the users' terminal.
#	Often this is useful to signal completion of a lengthy script
#	or to prompt the user to enter information.  Multiple beeps
#	are separated by 1 second delays.
#
# Usage:
#	beep [n]
# 
# Arguments:
#	n = number of times to beep (default 1)
# 
# Output:
#	none
#
# Side effects:
#	The terminal beeps.
# 
# Example:
#	1) generate a single beep:
#		beep
#	2) generate 10 beeps:
#		beep 10
#
#-
# Modification history:
#	2012-Apr-02	GDW	Adapted for MOSFIRE from DEIMOS bells command
#-----------------------------------------------------------------------

set cmd = `basename $0`
@ n = 1 # default to 1 beep

# check args...
if (${#argv} > 1) then
    printf "Usage: $cmd [n]\n\a"
    exit 1
endif

# parse args...
if ( ${#argv} >0 ) then
    @ n = $1
    shift
endif

# loop...
@ i = 1
while ($i <= $n)

  printf "\a"

  # delay...
  if ( $i < $n ) sleep 1
    
  @ i++
end
