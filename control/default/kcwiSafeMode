#!/bin/csh -f
#+
# KCWI library of scripts
#
# NAME
#     kcwiSafeMode - puts the instrument in a safe mode for detector
#
# SYNOPSIS
#       kcwiSafeMode
#
# DESCRIPTION
#       This script will put the instrument into a safe mode.
#       - close the dust cover (front hatch)
#       - inserts a dark filter
#
# ARGUMENTS
#       none
#
# OPTIONS
#       none
#
# EXAMPLES
#
# ENVIRONMENT VARIABLES
#
# FILES
#
# SERVERS & KEYWORDS
#
# SCRIPTS CALLED
#
# EXIT STATUS
#     0 - normal exit, no error
#     1 - script aborted by an interrupt
#
# Modification History:
#	2013-Apr-28	GDW	Original version
#       2016-Jul-18     LR      Modified for KCWI
#------------------------------------------------------------------------------

set cmd = `basename $0`

# move the filter...
echo "----------------------------------------"
echo "Inserting a dark filter"
echo "----------------------------------------"

@ i = 0
@ imax = 10
set keepgoing = 1
while ( $keepgoing )

    @ i++
    set_state -v <<EOF
kcwi  OBSMODE   Dark-spectroscopy    
EOF

    # check for success...
    if ( $status == 0 ) then
	set keepgoing = 0
    else
	printf "[$cmd] WARNING: DARK filter move failed (attempt $i/$imax)\n"
	if ( $i >= $imax ) then
	    printf "[$cmd] ERROR: exceeded max attempts on DARK filter move!\n\a"
	    set fail
	endif
    endif
end

# close the dust cover using the script because 
# it ensures that the keyword server is unlocked 
# before closing.
echo "----------------------------------------"
echo "Closing the hatch"
echo "----------------------------------------"

@ i = 0
@ imax = 10
set keepgoing = 1
while ( $keepgoing )

    @ i++
    dustclose

    # check for success...
    if ( $status == 0 ) then
	set keepgoing = 0
    else
	printf "[$cmd] WARNING: KCWI hatch move failed (attempt $i/$imax)\n\a"
	if ( $i >= $imax ) then
	    printf "[$cmd] ERROR: exceeded max attempts on KCWI hatch move!\n\a"
	endif
    endif
end

# exit if no failures...
if ( ! $?fail ) then
    exit 0
endif

# get current state...
set hatchpos = `show -s kcwi -terse mdcname`
set obsmode  = `show -s kcwi -terse obsmode`

# send warning email...
mailx -s "Failed to put KCWI into SAFE mode" \
    kcwi_info@keck.hawaii.edu <<EOF

WARNING: the $cmd script failed to put KCWI into "safe mode" to
protect the detector.  The mode we want KCWI in is:

   HATCHPOS = closed
   OBSMODE  = Dark-spectroscopy or Dark-imaging

but the current settings are:

   HATCHPOS = $hatchpos
   OBSMODE  = $obsmode

Please do the following (or contact a Support Astronomer) to put
KCWI into safe mode:

  1. Log into kcwiserver (ssh moseng@kcwiserver) .
  2. Run 'testAll'.
  3. Check output and verify that all servers are healthy; if not, then 
     fix them.
  4. Execute the command 'kcwiSafeMode' to ensure that the KCWI 
     hatch is closed and the filter is set to the dark position.

------------------------------------------------------------------------
Generator: $cmd
Account:   `whoami`
Time:      `timestamp`
EOF

exit 1

